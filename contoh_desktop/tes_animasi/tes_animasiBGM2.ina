# ################################################
# tes Animasi program dengan BAIK6
# Detak Waktu pada layar - Auto Double Buffering
# ################################################
global ekspos1
ekspos1 = 0
global pix, pos_x, pos_y, pos_w, pos_h

global x1, y1
x1 = 150
y1 = 300

# ################################################

pakai_layar

lyr1 = layar
titel_layar lyr1, "Animasi Pesawat Dgn Papan Ketik plus BGM"
luas_layar  lyr1, 500, 550
logo_layar  lyr1, "baik_logo.png"

alas1 = alas_tegak
hias_layar lyr1, alas1

papan = papan_menu
# -------------------------
mn01  = menu

mnMenu1  = isi_menu "Keluar"
mnKeluar = isi_menu "Selesai"

submenu       mnMenu1, mn01
 tambah_menu  mn01, mnKeluar
akhir_menu    papan, mnMenu1

# -------------------------
susun_menu alas1, papan

tutup_program lyr1
aksi_tutup_program mnKeluar

lyr2 = layar_gulung
hias_layar alas1, lyr2

lyrkanvas = kanvas
hias_layar lyr2, lyrkanvas

aksi_ekspos           lyr1, &lakukan1(lyrkanvas)
aksi_tekan_papanketik lyr1, &myketikan(lyrkanvas)

musik_penghias "airwolf2.wav"
detak_waktu 200,lyr1, &pewaktu(lyrkanvas)

tampil_layar lyr1
kibar_layar

TAMAT
# ################################


# ################################
# Timer
# ################################
Fungsi pewaktu(lyrkanvas1) {
  # pakai pixmap untuk layar penyangga
  hapus_pixmap pix
  xx = alokasi_lebar  lyrkanvas1
  yy = alokasi_tinggi lyrkanvas1
  pix = bidang_pixmap lyrkanvas1, xx, yy, -1

  ltr = latar_layar  pix, 0.0, 0.0, 0.0

  # komponen berubah terhadap waktu

  # buat bidang gambar dari PNG
  pnl2 = bidang_ambilpng "BAIK_kapal2.png"

  set_asalbidang ltr, pnl2, x1, y1
  lukis_pena ltr
  hapus_bidang  pnl2




  warna_pena ltr, 0.0, 1.0, 1.0
  atribut_teks ltr, 12, "Sans", "tebal"
  lukis_teks ltr, 10, 50, "Gerakkan Pesawat Dgn Tombol Panah"
  lukis_teks ltr, 10, 65, "<-  kiri"
  lukis_teks ltr, 10, 80, "->  kanan"
  lukis_teks ltr, 10, 95, "^   atas"
  lukis_teks ltr, 10, 110, "v   bawah"
  hapus_latar    ltr

  lukis_pixmap lyrkanvas1, pix, pos_x, pos_y, pos_x, pos_y, pos_w, pos_h
}

# ################################
# Aksi Ekspos
# ################################
Fungsi lakukan1(lyrkanvas1) {
  # redrawing only bidang penyangga (pixmap)

  kalau ekspos1 == 0 maka
    hapus_pixmap pix
    xx = alokasi_lebar  lyrkanvas1
    yy = alokasi_tinggi lyrkanvas1
    pix = bidang_pixmap lyrkanvas1, xx, yy, -1

  ltr = latar_layar  pix, 0.0, 0.0, 0.0

    pos_x = area_x
    pos_y = area_y
    pos_w = area_lebar
    pos_h = area_tinggi
    lukis_pixmap lyrkanvas1, pix, pos_x, pos_y, pos_x, pos_y, pos_w, pos_h

    ekspos1++
  akhirkalau

}

Fungsi myketikan(lyrkanvas2) {
  tmb   = ketikan_nilai
  kiri  = PANAH_KIRI
  kanan = PANAH_KANAN
  atas  = PANAH_ATAS
  bawah = PANAH_BAWAH
  kalau tmb == kiri maka
    x1 = x1 - 30
  lainkalau tmb == kanan maka
    x1 = x1 + 30
  lainkalau tmb == atas maka
    y1 = y1 - 30
  lainkalau tmb == bawah maka
    y1 = y1 + 30
  akhirkalau

}

